#+TITLE:       Note taking 3: Notes from elfeed entries
#+AUTHOR:      Heikki Lehv√§slaiho
#+EMAIL:       heikki.lehvaslaiho@gmail.com
#+DATE:        2015-05-09 Sat
#+URI:         /blog/%y/%m/%d/notes-from-elfeed-entries
#+KEYWORDS:    notes, elfeed, bookmark
#+TAGS:        emacs
#+LANGUAGE:    en
#+OPTIONS:     H:3 num:nil toc:nil \n:nil ::t |:t ^:nil -:nil f:t *:t <:t
#+DESCRIPTION: How to create an org note of an elfeed entry

This is the third article in the series on quick note taking using
emacs. It shows an quick way of creating a note for the current
entry in elfeed news reader with one key press.

1. [[http://heikkil.github.io/blog/2015/05/07/notes-in-org-agenda/][Org agenda with date tree file]]
2. [[http://heikkil.github.io/blog/2015/05/08/notes-from-browser-window/][Notes from browser window]]
3. Notes from elfeed entries

* Elfeed

[[https://github.com/skeeto/elfeed][Elfeed]] is a web feed reader that is written in emacs lisp that makes
it [[http://heikkil.github.io/blog/2015/02/24/custom-elfeed-filter-functions/][easy to modify]] and extend for different work flows. I use it with
[[ttps://github.com/remyhonig/elfeed-org][elfeed-org]] package to follow dozens of web sites. Every now and
then I do not want to open an entry in the browser but to make a quick
note of it into my [[http://heikkil.github.io/blog/2015/05/07/notes-in-org-agenda/][org notes]] directly from  elfeed.

I am now using a previously unbound key =v= to paste a link to the
appropriate place in my date tree using org-capture. For situations
where I want to paste the link to some other text, I use letter =l= to
copy the org link to clipboard.

Elfeed has two main modes, elfeed-search and elfeed-show, that need
separate functions tailored for them. The elfeed-search buffer
shows titles of currently filtered entries, and the elfeed-show buffer
renders (part of) the entry itself.

** Copy the link string

I first define a generic function elfeed-link-title that takes an
elfeed entry object as an argument. It creates the org link from the
URL and the title of the entry and places the string in the clipboard.

#+BEGIN_SRC emacs-lisp
       (defun elfeed-link-title (entry)
         "Copy the entry title and URL as org link to the clipboard."
         (interactive)
         (let* ((link (elfeed-entry-link entry))
                (title (elfeed-entry-title entry))
                (titlelink (concat "[[" link "][" title "]]")))
           (when titlelink
             (kill-new titlelink)
             (x-set-selection 'PRIMARY titlelink)
             (message "Yanked: %s" titlelink))))
#+END_SRC

** Elfeed-show mode

The elfeed-show mode link command simply calls that function with the
current entry. The note command does the same and uses the note org
capture function defined in [[the first article in this series]] to store
the link.

I use bind-keys from [[https://github.com/jwiegley/use-package][use-package]] to bind keys in the correct key map
to these functions.

#+BEGIN_SRC emacs-lisp
       (defun elfeed-show-link-title ()
         "Copy the current entry title and URL as org link to the clipboard."
         (interactive)
         (elfeed-link-title elfeed-show-entry))

       (defun elfeed-show-quick-url-note ()
         "Fastest way to capture entry link to org agenda from elfeed show mode"
         (interactive)
         (elfeed-link-title elfeed-show-entry)
         (org-capture nil "n")
         (yank)
         (org-capture-finalize))

       (bind-keys :map elfeed-show-mode-map
                  ("l" . elfeed-show-link-title)
                  ("v" . elfeed-show-quick-url-note)))
#+END_SRC

** Elfeed-search mode

The elfeed-search mode can have several entries selected, so the
functions need to loop over them although selecting multiple entries
is not very useful in this context. Creating a link to an entry has
the same side effects than opening it in a browser: The entry is mark
as read and the focus in moved to the next line.

#+BEGIN_SRC emacs-lisp
       (defun elfeed-search-link-title ()
         "Copy the current entry title and URL as org link to the clipboard."
         (interactive)
         (let ((entries (elfeed-search-selected)))
           (cl-loop for entry in entries
                    when (elfeed-entry-link entry)
                    do (elfeed-link-title entry))))

       (defun elfeed-search-quick-url-note ()
         "In search mode, capture the title and link for the selected
     entry or entries in org aganda."
         (interactive)
         (let ((entries (elfeed-search-selected)))
           (cl-loop for entry in entries
                    do (elfeed-untag entry 'unread)
                    when (elfeed-entry-link entry)
                    do (elfeed-link-title entry)
                    do (org-capture nil "n")
                    do (yank)
                    do (org-capture-finalize)
                    (mapc #'elfeed-search-update-entry entries))
           (unless (use-region-p) (forward-line))))

       (bind-keys :map elfeed-search-mode-map
                  ("l" . elfeed-search-link-title)
                  ("v" . elfeed-search-quick-url-note))
#+END_SRC

* Epilogue

For the above code snippets to be fully functional they need to be within
[[https://github.com/jwiegley/use-package][use-package]] macro as in [[https://gist.github.com/heikkil/1feaf53133b56e5c51e1][this gist]]. Otherwise the key bindings will fail.
